title: Level1Start
tags: load
---
5 years later...
Amsel and Sate have finished their education. Sate works in their father's hospital and Amsel works in the primary wheat farm of the town.
The day ends, along with Amsel's shift. The two of you approach his father to ask him a favor...
(Note - you don't have to do this. You can go out and explore!)
AMSEL: Let's knock on his door.
<<enddialogue "Game Manager">>
===

title: Level1Intro
tags: intro targetstart
---
<<setcharacter "Game Manager" "Doctor">>
<<setmultiplier "Game Manager" 1>>
<<settarget "Advance" 3>>
<<setactive "Advance" 0>>
<<set $mood to "">>
<<set $choice to "">>
<<set $response to "">>
<<randomisemood "Game Manager">>
<<focustarget "Advance" true>>
<<if $mood == "positive">>
    He seems to have a small smile on his face.
<<elseif $mood == "neutral">>
    Well, he seems indifferent today.
<<else>>
    He is glaring into the wall.
<<endif>>
<<focustarget "Advance" false>>
<<setactive "Advance" 3>>
<<callkeywords "Game Manager">>
AMSEL: Hmm...
AMSEL: {$choice}
<<focustarget "Advance" true>>
DR. ANTONI: {$response}
<<setactive "Advance" 0>>
You excuse yourself for a second to talk with your friend.
<<jumpstart "After Intro" "Level1AfterIntro">>
===

title: Level1AfterIntro
tags: immediate
---
<<togglefriend "Game Manager">>
SATE: Hmmm...
SATE: Father has always been rather serious. If he is asking us for a deeper explanation, then we really should think it over.
SATE: Maybe looking around can help clear our head, and maybe even give us some clues!
<<togglefriend "Game Manager">>
<<enddialogue "Game Manager">>
===

title: NeatlyStackedPapers
tags: clue
---
(Clue found - neatly stacked papers)
AMSEL: Not a crease to be seen on them, with neat handwriting and consistent ordering.
<<addclue "Game Manager" "Neatly Stacked Papers" "Not a crease to be seen on them, with neat handwriting and consistent ordering." "DR. ANTONI">>
<<enddialogue "Game Manager">>
===

title: StickyNotes
tags: clue
---
(Clue found - sticky notes)
AMSEL: “Meet with the mayor to request funding for imports of medicine - again.” 
AMSEL: Scribbled below is a short postscript. “He never listens.”
<<addclue "Game Manager" "Sticky Notes" "'Meet with the mayor to request funding for imports of medicine - again.' Scribbled below is a short postscript. 'He never listens.' " "DR. ANTONI">>
<<enddialogue "Game Manager">>
===

title: Level1Persuasion
tags: persuasion targetstart
---
<<setmultiplier "Game Manager" 2>>
<<set $persuasion to 30>>
<<set $choice to "">>
<<set $response to "">>
<<settarget "Advance" 3>>
<<setactive "Advance" 3>>
<<focustarget "Advance" true>>
DR. ANTONI: Alright. Your explanation…?
<<focustarget "Advance" false>>
<<callkeywords "Game Manager">>
AMSEL: Hmm...
AMSEL: {$choice}
<<focustarget "Advance" true>>
DR. ANTONI: {$response}
<<setactive "Advance" 0>>
<<focustarget "Advance" true>>
<<checkpersuasion "Game Manager">>
<<if $persuasion >= 50>>
    It seems like he’s growing more and more convinced of the idea, but you can say more.
<<elseif $persuasion < 50>>
    You see a growing frown on his face, but you can still say more.
<<endif>>
<<setactive "Advance" 3>>
<<focustarget "Advance" false>>
<<callkeywords "Game Manager">>
AMSEL: Hmm...
AMSEL: {$choice}
<<focustarget "Advance" true>>
DR. ANTONI: {$response}
DR. ANTONI: Errr…
DR. ANTONI: Child, you understand how important this is?
<<setmainspeaker "Advance" 2>>
SATE: Yes, Father. That is why I still believe in doing this.
SATE: You know, no matter what you say, I’ll still do it.

<<set $success to false>>
<<rollsuccess "Game Manager">>

<<if $success == true>>
    DR. ANTONI: Alright, fine. Just tell me what you need. I’ll go talk to the staff.
    <<unlockitems "Game Manager">>
    <<setactive "Advance" 0>>
    You have unlocked 'promotional items'.
    Click them at the end of a dialogue to give them to your target.
    There are three types - pamphlets, posters, and assorted items.
    These give a 5, 10, and 15% bonus to your persuasion score respectively.
    You can give more than one item if you wish.
    Please note, however, that you only get a limited number of each type.
<<else>>
    DR. ANTONI: You’re on your own. Prove that your cause is worth it.
<<endif>>
<<setmainspeaker "Advance" 1>>
<<enddialogue "Game Manager">>
<<endlevel "Game Manager">>
===